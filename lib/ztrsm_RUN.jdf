extern "C" %{
/*
 *  Copyright (c) 2010-2013
 *
 *  The University of Tennessee and The University
 *  of Tennessee Research Foundation.  All rights
 *  reserved.
 *
 * @precisions normal z -> s d c
 *
 */
#include "dplasma/lib/dplasmajdf.h"
#include "data_dist/matrix/matrix.h"

%}

side      [type = "PLASMA_enum"]
uplo      [type = "PLASMA_enum"]
trans     [type = "PLASMA_enum"]
diag      [type = "PLASMA_enum"]
alpha     [type = "dague_complex64_t"]
descA     [type = "tiled_matrix_desc_t*"]
descB     [type = "tiled_matrix_desc_t*"]


ztrsm(k,m)
  /* Execution space */
  k = 0..(descB->nt-1)
  m = 0..(descB->mt-1)

  : descB(m,k)

  READ  A <- A ztrsm_in_A0(k)

  RW    B <- (0==k) ? descB(m,k)
          <- (k>=1) ? E zgemm(k-1, m, k)
          -> (descB->nt>=(k+2)) ? C zgemm(k, m, (k+1) .. (descB->nt-1))
          -> descB(m,k)

BODY
{
    int tempmm = ((m)==(descB->mt-1)) ? (descB->m-(m*descB->mb)) : descB->mb;
    int tempkn = ((k)==(descB->nt-1)) ? (descB->n-(k*descB->nb)) : descB->nb;
    dague_complex64_t lalpha = ((k)==(0)) ? (alpha) : (dague_complex64_t)1.0;
    int lda = BLKLDDP( descA, k );
    int ldb = BLKLDDP( descB, m );

#if !defined(DAGUE_DRY_RUN)
        CORE_ztrsm(side, uplo, trans, diag,
                   tempmm, tempkn, lalpha,
                   A /* descA(k,k) */, lda,
                   B /* descB(m,k) */, ldb );
#endif /* !defined(DAGUE_DRY_RUN) */

    printlog("CORE_ztrsm(%d, %d)\n"
             "\t(side, uplo, trans, diag, tempmm, tempkn, lalpha, descA(%d,%d)[%p], lda, descB(%d,%d)[%p], ldb)\n",
             k, m, k, k, A, m, k, B);
}
END

/*
 * Pseudo-task
 */
ztrsm_in_A0(k) [profile = off]
  k = 0 .. (descB->nt-1)

  : descA(k,k)

  RW A <- descA(k,k)
       -> A ztrsm(k,0 .. (descB->mt-1))
BODY
{
    /* nothing */
}
END


zgemm(k,m,n)
  /* Execution space */
  k = 0 .. (descB->nt-2)
  m = 0 .. (descB->mt-1)
  n = (k+1) .. (descB->nt-1)

  : descB(m,n)

  READ  C <- B ztrsm(k, m)
  READ  D <- D zgemm_in_A0(k,n)

  RW    E <- (0==k) ? descB(m,n)
          <- (k>=1) ? E zgemm(k-1, m, n)
          -> ((1+k)==n) ? B ztrsm(n, m)
          -> (n>=(2+k)) ? E zgemm(k+1, m, n)

BODY
{
    int tempmm = ((m)==(descB->mt-1)) ? (descB->m-(m*descB->mb)) : descB->mb;
    int tempnn = ((n)==(descB->nt-1)) ? (descB->n-(n*descB->nb)) : descB->nb;
    int ldb = BLKLDDP( descB, m );
    int lda = BLKLDDP( descA, k );

    dague_complex64_t lalpha = ((k)==(0)) ? (alpha) : (dague_complex64_t)1.0;

#if !defined(DAGUE_DRY_RUN)
        CORE_zgemm(PlasmaNoTrans, PlasmaNoTrans,
                   tempmm, tempnn, descB->mb,
                   -1.0,   C /* descB(m,k) */, ldb,
                           D /* descA(k,n) */, lda,
                   lalpha, E /* descB(m,n) */, ldb );
#endif /* !defined(DAGUE_DRY_RUN) */

    printlog("CORE_zgemm(%d, %d, %d)\n"
             "\t(PlasmaNoTrans, PlasmaNoTrans, tempmm, tempnn, descB->mb, mzone, descB(%d,%d)[%p], ldb, descA(%d,%d)[%p], lda, lalpha, descB(%d,%d)[%p], ldb)\n",
             k, m, n, m, k, C, k, n, D, m, n, E);
}
END

/*
 * Pseudo-task
 */
zgemm_in_A0(k,n) [profile = off]
  k = 0 .. (descB->nt-2)
  n = (k+1) .. (descB->nt-1)

: descA(k, n)

    RW D <- descA(k, n)
       -> D zgemm(k,0 .. (descB->mt-1),n)
BODY
{
    /* nothing */
}
END
